// Copyright 2021 Cartesi Pte. Ltd.

// SPDX-License-Identifier: Apache-2.0
// Licensed under the Apache License, Version 2.0 (the "License"); you may not use
// this file except in compliance with the License. You may obtain a copy of the
// License at http://www.apache.org/licenses/LICENSE-2.0

// Unless required by applicable law or agreed to in writing, software distributed
// under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
// CONDITIONS OF ANY KIND, either express or implied. See the License for the
// specific language governing permissions and limitations under the License.

/// @title Specific ERC20 Portal Implementation
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";

import "./SERC20Portal.sol";
import "./Input.sol";

contract SERC20PortalImpl is SERC20Portal {
    address immutable voucherContract;
    Input immutable inputContract;
    IERC20 immutable erc20Contract;

    modifier onlyVoucherContract {
        require(msg.sender == voucherContract, "only voucherContract");
        _;
    }

    constructor(address _inputContract, address _voucherContract, address _erc20Contract) {
        inputContract = Input(_inputContract);
        voucherContract = _voucherContract;
        erc20Contract = IERC20(_erc20Contract);
    }

    /// @notice deposit an amount of a specific ERC20 token in the portal contract and create tokens in L2
    /// @param _amount amount of the ERC20 token to be distributed
    /// @param _data information to be interpreted by L2
    /// @return hash of input generated by deposit
    function erc20Deposit(
        uint256 _amount,
        bytes calldata _data
    ) public override returns (bytes32) {
        require(
            erc20Contract.transferFrom(msg.sender, address(this), _amount),
            "ERC20 transferFrom failed"
        );

        bytes memory input =
            abi.encode(
                msg.sender,
                _amount,
                _data
            );

        emit SERC20Deposited(msg.sender, _amount, _data);
        return inputContract.addInput(input);
    }

    /// @notice executes a rollups voucher
    /// @param _data data with information necessary to execute voucher
    /// @dev can only be called by Voucher contract
    function executeRollupsVoucher(bytes calldata _data)
        public
        override
        onlyVoucherContract
        returns (bool)
    {
        (
            address payable receiver,
            uint256 value
        ) = abi.decode(_data, (address, uint256));

        // transfer reverts on failure
        erc20Contract.transfer(receiver, value);

        emit SERC20Withdrawn(receiver, value);

        return true;
    }
}
